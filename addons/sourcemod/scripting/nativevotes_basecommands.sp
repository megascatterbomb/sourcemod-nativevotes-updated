/**
 * vim: set ts=4 :
 * =============================================================================
 * NativeVotes Basecommands Plugin
 * Provides cancelvote and revote functionality for NativeVotes
 *
 * NativeVotes (C) 2011-2014 Ross Bemrose (Powerlord). All rights reserved.
 * SourceMod (C)2004-2008 AlliedModders LLC.  All rights reserved.
 * =============================================================================
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of the GNU General Public License, version 3.0, as published by the
 * Free Software Foundation.
 * 
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more
 * details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 * As a special exception, AlliedModders LLC gives you permission to link the
 * code of this program (as well as its derivative works) to "Half-Life 2," the
 * "Source Engine," the "SourcePawn JIT," and any Game MODs that run on software
 * by the Valve Corporation.  You must obey the GNU General Public License in
 * all respects for all other code used.  Additionally, AlliedModders LLC grants
 * this exception to all derivative works.  AlliedModders LLC defines further
 * exceptions, found in LICENSE.txt (as of this writing, version JULY-31-2007),
 * or <http://www.sourcemod.net/license.php>.
 *
 * Version: $Id$
 */
 
#include <sourcemod>
#include <nativevotes>

#undef REQUIRE_PLUGIN
#include <adminmenu>

#pragma newdecls required
#pragma semicolon 1

#define PLUGIN_PREFIX "[\x04NativeVotes\x01]"

TopMenu hTopMenu;

public Plugin myinfo = 
{
	name = "NativeVotes | Basic Commands",
	author = "Powerlord and AlliedModders LLC",
	description = "Revote and Cancel support for NativeVotes",
	version = "26w07a",
	url = "https://github.com/Heapons/sourcemod-nativevotes-updated/"
}

public void OnPluginStart()
{
	LoadTranslations("core.phrases");
	LoadTranslations("common.phrases");
	
	AddCommandListener(Command_CancelVote, "sm_cancelvote");
	AddCommandListener(Command_ReVote, "sm_revote");
	RegConsoleCmd("sm_callvote", Command_CallVote, "Start a vote on an issue.");
}

bool PerformCancelVote(int client)
{
	if (!NativeVotes_IsVoteInProgress())
	{
		return false;
	}

	CShowActivity2(client, "[\x04NativeVotes\x01] ", "%t", "Cancelled Vote");
	
	NativeVotes_Cancel();
	return true;
}

public void OnClientSayCommand_Post(int client, const char[] command, const char[] sArgs)
{
	if (!client || IsChatTrigger())
	{
		return;
	}
	
	if (strcmp(sArgs, "callvote", false) == 0)
	{
		ReplySource old = SetCmdReplySource(SM_REPLY_TO_CHAT);
		
		Command_CallVote(client, 0);
		
		SetCmdReplySource(old);
	}
}

public Action Command_CancelVote(int client, const char[] command, int argc)
{
	if (!CheckCommandAccess(client, "sm_cancelvote", ADMFLAG_VOTE))
	{
		if (IsVoteInProgress())
		{
			// Let basecommands handle it
			return Plugin_Continue;
		}
		
		CReplyToCommand(client, "%t", "No Access");
		return Plugin_Stop;
	}
	
	if (PerformCancelVote(client))
	{
		return Plugin_Stop;
	}
	else
	{
		return Plugin_Continue;
	}
}

public void AdminMenu_CancelVote(Handle topmenu, TopMenuAction action, TopMenuObject object_id, int param, char[] buffer, int maxlength)
{
	switch (action)
	{
		case TopMenuAction_DisplayOption:
		{
			Format(buffer, maxlength, "%T", "Cancel vote", param);
		}
		case TopMenuAction_SelectOption:
		{
			PerformCancelVote(param);
			RedisplayAdminMenu(topmenu, param);	
		}
		case TopMenuAction_DrawOption:
		{
			buffer[0] = NativeVotes_IsVoteInProgress() ? ITEMDRAW_DEFAULT : ITEMDRAW_IGNORE;
		}
	}
}

public Action Command_ReVote(int client, const char[] command, int argc)
{
/*
    if (FindSendPropInfo("CVoteController", "m_nVoteIdx") > 0)
	{
		LogError("[NativeVotes] Re-voting is buggy in Team Fortress 2 and SDK 2025 mods.");
		return Plugin_Continue;
	}
*/
	if (client <= 0)
	{
		return Plugin_Continue;
	}
	
	if (!NativeVotes_IsVoteInProgress())
	{
		return Plugin_Continue;
	}
	
	if (!NativeVotes_IsClientInVotePool(client))
	{
		if (IsVoteInProgress())
		{
			// Let basecommands handle it
			return Plugin_Continue;
		}
		
		CReplyToCommand(client, PLUGIN_PREFIX ... " %t", "Cannot participate in vote");
		return Plugin_Stop;
	}
	
	if (NativeVotes_RedrawClientVote(client))
	{
		return Plugin_Stop;
	}
	else if (!IsVoteInProgress())
	{
		CReplyToCommand(client, PLUGIN_PREFIX ... " %t", "Cannot change vote");
		return Plugin_Stop;
	}
	
	return Plugin_Continue;
}

public Action Command_CallVote(int client, int args)
{
	if (client <= 0)
	{
		return Plugin_Continue;
	}
	
	FakeClientCommand(client, "callvote");
	
	return Plugin_Continue;
}

public void OnAdminMenuReady(Handle aTopMenu)
{
	TopMenu topmenu = TopMenu.FromHandle(aTopMenu);
	
	/* Block us from being called twice */
	if (topmenu == hTopMenu)
	{
		return;
	}
	
	/* Save the Handle */
	hTopMenu = topmenu;
	
	TopMenuObject voting_commands = hTopMenu.FindCategory(ADMINMENU_VOTINGCOMMANDS);

	if (voting_commands != INVALID_TOPMENUOBJECT)
	{
		hTopMenu.AddItem("sm_cancelvote", AdminMenu_CancelVote, voting_commands, "sm_cancelvote", ADMFLAG_VOTE);
	}
}

public void OnLibraryRemoved(const char[] name)
{
	if (strcmp(name, "adminmenu") == 0)
	{
		hTopMenu = null;
	}
}